{
  description = "Chisel Nix";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable"; # 基础包集
    flake-parts = { # 按系统拆分 outputs 的框架
      url = "github:hercules-ci/flake-parts";
      inputs.nixpkgs-lib.follows = "nixpkgs";
    };
    treefmt-nix = { # 代码格式化工具
      url = "github:numtide/treefmt-nix";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    zaozi.url = "github:sequencer/zaozi"; # MLIR/CIRCT 相关工具链
    mill-ivy-fetcher.url = "github:Avimitin/mill-ivy-fetcher"; # 将 mill 的 ivy 依赖, 锁定到 nix store
  };

  outputs =
    inputs@{
      self,
      nixpkgs,
      flake-parts,
      ...
    }:
    let
      overlay = import ./nix/overlay.nix;
    in
    flake-parts.lib.mkFlake { inherit inputs; } {
      systems = [
        "x86_64-linux"
        "aarch64-linux"
        "aarch64-darwin"
      ];

      imports = [
        inputs.treefmt-nix.flakeModule
      ];

      flake.overlays.default = overlay;

      perSystem =
        { system, pkgs, ... }:
        {
          _module.args.pkgs = import nixpkgs {
            inherit system;
            # TODO: Do not depend on overlay of zaozi in favor of importing its outputs explicitly to avoid namespace pollution.
            overlays = with inputs; [
              zaozi.overlays.default
              mill-ivy-fetcher.overlays.default
              overlay
            ];
          };

          legacyPackages = pkgs;

          treefmt = {
            projectRootFile = "flake.nix";
            programs.scalafmt = {
              enable = true;
              includes = [ "*.mill" ];
            };
            programs.nixfmt = {
              enable = true;
              excludes = [ "*/generated.nix" ];
            };
          };

          devShells.default =
            with pkgs;
            mkShell (
              {
                inputsFrom = [ sdram.sdram-compiled ];
                packages = [
                  nixd
                  nvfetcher
                  verilator
                  python3
                  clang-tools
                  gtkwave
                  ccache
                  bear
                  metals
                ];
                OBJCACHE = "ccache";
              }
              // sdram.sdram-compiled.env
            );
        };
    };
}
